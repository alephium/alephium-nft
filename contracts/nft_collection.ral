// The concept of a collection, which could contain different NFTs
// e.g. Bored Ape Yacht Club collection
TxContract NFTCollection(
    nftByteCode: ByteVec,
    collectionName: ByteVec,
    collectionDescription: ByteVec,
    collectionUri: ByteVec
) {
    event NFTMinted(
      minter: Address,
      collectionAddress: Address,
      name: ByteVec,
      description: ByteVec,
      uri: ByteVec,
      tokenId: ByteVec,
      nftContractAddress: Address
    )

    @using(preapprovedAssets = true, assetsInContract = true)
    pub fn mint(
        nftName: ByteVec,
        nftDescription: ByteVec,
        nftUri: ByteVec
    ) -> (ByteVec) {
        let minter = callerAddress!()

        let collectionAddress = selfAddress!()

        let initialState = encodeToByteVec!(
            minter,
            nftName,
            nftDescription,
            nftUri,
            collectionAddress
        )

        approveAlph!(minter, 1000000000000000000)
        let contractId = createSubContractWithToken!(nftUri, nftByteCode, initialState, 1)

        emit NFTMinted(
            minter,
            collectionAddress,
            nftName,
            nftDescription,
            nftUri,
            contractId,
            contractIdToAddress!(contractId)
        )

        return contractId
    }
}