// This is for the scenario where an artist or AI creates a set of arts
// in advance and sell to the public for minting, users can mint the arts
// in the collection sequentially by paying the mint price.
Abstract Contract NFTPublicSaleCollectionSequentialBase(
    nftTemplateId: ByteVec,
    collectionUri: ByteVec,
    nftBaseUri: ByteVec,
    collectionOwner: Address,
    maxSupply: U256,
    mintPrice: U256,
    maxBatchMintSize: U256,
    mut totalSupply: U256
) extends NFTCollectionBase(collectionUri, totalSupply), NFTPublicSaleCollectionBase(nftBaseUri, maxSupply, mintPrice){
    event Mint(minter: Address, fromIndex: U256, mintSize: U256)

    enum ErrorCodes {
        IncorrectTokenIndex = 2
        InvalidMintBatchSize = 3
        InsufficientNumOfUnminted = 4
    }

    @using(preapprovedAssets = true)
    fn mint_(minter: Address, index: U256) -> ByteVec {
        let (encodeImmutableFields, encodeMutableFields) = NFT.encodeFields!(getNFTUri(index), selfContractId!(), index)
        return copyCreateSubContractWithToken!{minter -> ALPH: 1 alph}(
            toByteVec!(index),
            nftTemplateId,
            encodeImmutableFields,
            encodeMutableFields,
            1,
            minter
        )
    }

    @using(preapprovedAssets = true, updateFields = true, assetsInContract = true, checkExternalCaller = false)
    pub fn mint() -> (ByteVec) {
        assert!(totalSupply < maxSupply, ErrorCodes.IncorrectTokenIndex)

        let minter = callerAddress!()
        let fromIndex = totalSupply
        let contractId = mint_{minter -> ALPH: 1 alph}(minter, fromIndex)

        transferTokenToSelf!(minter, ALPH, mintPrice)

        totalSupply = totalSupply + 1

        emit Mint(minter, fromIndex, 1)
        return contractId
    }

    @using(preapprovedAssets = true, updateFields = true, assetsInContract = true, checkExternalCaller = false)
    pub fn mintBatch(size: U256) -> ByteVec {
        assert!(size <= maxBatchMintSize, ErrorCodes.InvalidMintBatchSize)
        assert!(size <= (maxSupply - totalSupply), ErrorCodes.InsufficientNumOfUnminted)

        let minter = callerAddress!()
        let fromIndex = totalSupply
        let mut contractIds = #
        for (let mut i = 0; i < size; i = i + 1) {
            contractIds = contractIds ++ mint_{minter -> ALPH: 1 alph}(minter, fromIndex + i)
        }

        transferTokenToSelf!(minter, ALPH, mintPrice * size)

        totalSupply = totalSupply + size

        emit Mint(minter, fromIndex, size)
        return contractIds
    }
}