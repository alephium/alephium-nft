// This is for the scenario where someone to create a collection and starts
// to mint NFTs one-by-one
Abstract Contract NFTOpenCollectionBase(
    nftTemplateId: ByteVec,
    collectionUri: ByteVec,
    collectionOwner: Address, // Only owner can mint
    mut totalSupply: U256
) extends NFTCollectionBase(collectionUri, totalSupply) {
    event Mint(minter: Address, index: U256)

    @using(preapprovedAssets = true, updateFields = true)
    pub fn mint(nftUri: ByteVec) -> (ByteVec) {
        let minter = callerAddress!()
        checkCaller!(minter == collectionOwner, ErrorCodes.CollectionOwnerAllowedOnly)
        let nftIndex = totalSupply
        let (encodedImmutableFields, encodedMutableFields) = NFT.encodeFields!(nftUri, selfContractId!(), nftIndex)

        let contractId = copyCreateSubContractWithToken!{minter -> ALPH: 1 alph}(
            toByteVec!(nftIndex),
            nftTemplateId,
            encodedImmutableFields,
            encodedMutableFields,
            1,
            minter
        )

        totalSupply = totalSupply + 1

        emit Mint(minter, nftIndex)
        return contractId
    }
}
