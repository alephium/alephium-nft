// This is for the scenario where someone to create a collection and starts
// to mint NFTs one-by-one
Contract NFTOpenCollection(
    nftTemplateId: ByteVec,
    collectionUri: ByteVec,
    collectionOwner: Address, // Only owner can mint
    mut totalSupply: U256
) extends NFTCollectionBase(collectionUri, totalSupply) {
    @using(preapprovedAssets = true, updateFields = true)
    pub fn mint(nftUri: ByteVec) -> (ByteVec) {
        let minter = callerAddress!()
        checkCaller!(minter == collectionOwner, ErrorCodes.TokenOwnerAllowedOnly)
        let (encodeImmutableFields, encodeMutableFields) = NFT.encodeFields!(selfContractId!(), nftUri, totalSupply)

        let contractId = copyCreateSubContractWithToken!{minter -> ALPH: 1 alph}(
            toByteVec!(totalSupply),
            nftTemplateId,
            encodeImmutableFields,
            encodeMutableFields,
            1,
            minter
        )

        totalSupply = totalSupply + 1
        return contractId
    }
}
