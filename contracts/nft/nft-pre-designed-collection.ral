// This is for the scenario where an artist or AI creates a set of arts
// in advance and sell to the public for minting
Contract NFTPreDesignedCollection(
    enumerableNftTemplateId: ByteVec,
    collectionUri: ByteVec,
    tokenBaseUri: ByteVec,
    collectionOwner: Address,
    mut totalSupply: U256
) extends NFTCollectionBase(collectionUri, totalSupply) {
    @using(preapprovedAssets = true, updateFields = true, checkExternalCaller = false)
    pub fn mint() -> (ByteVec) {
        let minter = callerAddress!()
        assert!(minter == collectionOwner, ErrorCodes.TokenOwnerAllowedOnly)
        let (encodeImmutableFields, encodeMutableFields) = EnumerableNFT.encodeFields!(selfContract!(), totalSupply)

        let contractId = copyCreateSubContractWithToken!{minter -> ALPH: 1 alph}(
            toByteVec!(totalSupply),
            enumerableNftTemplateId,
            encodeImmutableFields,
            encodeMutableFields,
            1,
            minter
        )

        totalSupply = totalSupply + 1
        return contractId
    }

    pub fn getTokenUri(index: U256) -> ByteVec {
        assert!(index < totalSupply, ErrorCodes.IncorrectTokenIndex)
        return tokenBaseUri ++ u256ToString!(index)
    }
}