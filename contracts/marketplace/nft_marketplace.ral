Contract NFTMarketPlace(
    nftListingTemplateId: ByteVec,
    mut admin: Address,
    mut commissionRate: U256   // basis point. e.g. 2.5% == 250 basis point
) extends NFTMarketPlaceUtils() {
    event NFTListed(
      price: U256,
      tokenId: ByteVec,
      tokenOwner: Address,
      listingContractId: ByteVec
    )

    event NFTSold(
      price: U256,
      tokenId: ByteVec,
      previousOwner: Address,
      newOwner: Address
    )

    event NFTListingCancelled(
      tokenId: ByteVec,
      tokenOwner: Address
    )

    event NFTListingPriceUpdated(
       tokenId: ByteVec,
       oldPrice: U256,
       newPrice: U256
    )

    event AdminUpdated(
        previous: Address,
        new: Address
    )

    event CommissionRateUpdated(
        previous: U256,
        new: U256
    )

    enum ErrorCodes {
        AdminAllowedOnly = 0
        TokenOwnerAllowedOnly = 1
    }

    @using(preapprovedAssets = true, assetsInContract = true, checkExternalCaller = false)
    pub fn buyNFT(
        tokenId: ByteVec,
        collectionId: ByteVec  // Do not need this after parentId instr is introduced
    ) -> () {
        let nftListingContractId = subContractId!(tokenId)
        let nftListing = NFTListing(nftListingContractId)
        let buyer = callerAddress!()
        let tokenOwner = nftListing.getTokenOwner()
        let price = nftListing.getPrice()
        let commissionRateOfListing = nftListing.getCommissionRate()
        let royaltyAmount = nftListing.royaltyAmount(collectionId)
        let priceAfterFee = getPriceAfterFee(price, commissionRateOfListing, royaltyAmount)

        nftListing.buy{buyer -> ALPH: priceAfterFee}(buyer, priceAfterFee)

        if (royaltyAmount > 0) {
            let collectionWithRoyalty = INFTCollectionWithRoyalty(collectionId)
            collectionWithRoyalty.payRoyalty{buyer -> ALPH: royaltyAmount}(buyer, royaltyAmount)
        }

        // Charge a commission for the marketplace
        if (commissionRate > 0) {
            let commission = (price * commissionRateOfListing) / 10000
            transferToken!(buyer, selfAddress!(), ALPH, commission)
        }

        emit NFTSold(price, tokenId, tokenOwner, buyer)
    }

    @using(preapprovedAssets = true, checkExternalCaller = false)
    pub fn listNFT(
        tokenId: ByteVec,
        collectionId: ByteVec,   // Do not need this after parentId instr is introduced
        price: U256,
        royalty: Bool
    ) -> (Address) {
        let royaltyAmount = getRoyaltyAmount(tokenId, collectionId, price, royalty)
        getPriceAfterFee(price, commissionRate, royaltyAmount)

        // Only owner can list the NFT
        let tokenOwner = callerAddress!()

        let (encodeImmutableFields, encodeMutableFields) = NFTListing.encodeFields!(tokenId, tokenOwner, selfContractId!(), commissionRate, royalty, price)
        // Create the listing contract
        let nftListingContractId = copyCreateSubContract!{tokenOwner -> ALPH: 1 alph, tokenId: 1}(
            tokenId, nftListingTemplateId, encodeImmutableFields, encodeMutableFields
        )

        emit NFTListed(
            price,
            tokenId,
            tokenOwner,
            nftListingContractId
        )

        return contractIdToAddress!(nftListingContractId)
    }

    pub fn cancelNFTListing(tokenId: ByteVec) -> () {
        let nftListingContractId = subContractId!(tokenId)
        let nftListing = NFTListing(nftListingContractId)
        let tokenOwner = nftListing.getTokenOwner()

        checkCaller!(callerAddress!() == tokenOwner, ErrorCodes.TokenOwnerAllowedOnly)

        nftListing.cancel()
        emit NFTListingCancelled(tokenId, tokenOwner)
    }

    pub fn updateNFTPrice(
        tokenId: ByteVec,
        collectionId: ByteVec,
        newPrice: U256
    ) -> () {
        let nftListingContractId = subContractId!(tokenId)
        let nftListing = NFTListing(nftListingContractId)

        let commissionRateOfListing = nftListing.getCommissionRate()
        let royaltyAmount = nftListing.royaltyAmount(collectionId)

        getPriceAfterFee(newPrice, commissionRateOfListing, royaltyAmount)
        checkCaller!(callerAddress!() == nftListing.getTokenOwner(), ErrorCodes.TokenOwnerAllowedOnly)

        let price = nftListing.getPrice()

        nftListing.updatePrice(newPrice)
        emit NFTListingPriceUpdated(tokenId, price, newPrice)
    }

    @using(updateFields = true)
    pub fn updateAdmin(newAdmin: Address) -> () {
        checkCaller!(callerAddress!() == admin, ErrorCodes.AdminAllowedOnly)

        admin = newAdmin
        emit AdminUpdated(admin, newAdmin)
    }

    @using(updateFields = true)
    pub fn updateCommissionRate(newCommissionRate: U256) -> () {
        checkCaller!(callerAddress!() == admin, ErrorCodes.AdminAllowedOnly)

        commissionRate = newCommissionRate
        emit CommissionRateUpdated(commissionRate, newCommissionRate)
    }

    @using(assetsInContract = true)
    pub fn withdraw(to: Address, amount: U256) -> () {
        checkCaller!(callerAddress!() == admin, ErrorCodes.AdminAllowedOnly)
        transferTokenFromSelf!(to, ALPH, amount)
    }
}
