Contract NFTListing(
    tokenId: ByteVec,
    tokenOwner: Address,
    marketContractId: ByteVec,
    commissionRate: U256,
    listingFee: U256,
    mut price: U256
) {

    enum ErrorCodes {
        MarketplaceAllowedOnly = 0
    }

    pub fn getTokenOwner() -> Address {
        return tokenOwner
    }

    pub fn getPrice() -> U256 {
        return price
    }

    pub fn getCommissionRate() -> U256 {
        return commissionRate
    }

    pub fn getListingFee() -> U256 {
        return listingFee
    }

    @using(preapprovedAssets = true, assetsInContract = true)
    pub fn buy(buyer: Address) -> () {
        checkCaller!(callerContractId!() == marketContractId, ErrorCodes.MarketplaceAllowedOnly)
        let priceAfterFee = NFTMarketPlace(marketContractId).checkListingPriceAfterFee(price, commissionRate, listingFee)
        transferToken!(buyer, tokenOwner, ALPH, priceAfterFee)
        transferTokenFromSelf!(buyer, tokenId, 1)
        destroySelf!(tokenOwner)
    }

    @using(assetsInContract = true)
    pub fn cancel() -> () {
        checkCaller!(callerContractId!() == marketContractId, ErrorCodes.MarketplaceAllowedOnly)
        destroySelf!(tokenOwner)
    }

    @using(updateFields = true)
    pub fn updatePrice(newPrice: U256) -> () {
        checkCaller!(callerContractId!() == marketContractId, ErrorCodes.MarketplaceAllowedOnly)
        price = newPrice
    }
}
