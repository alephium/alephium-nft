Contract NFTListing(
    tokenId: ByteVec,
    tokenOwner: Address,
    marketAddress: Address,
    mut price: U256
) {

    enum ErrorCodes {
        MarketplaceAllowedOnly = 0
    }

    pub fn getTokenOwner() -> Address {
        return tokenOwner
    }

    pub fn getPrice() -> U256 {
        return price
    }

    @using(preapprovedAssets = true, assetsInContract = true)
    pub fn buy(buyer: Address) -> () {
        checkCaller!(callerAddress!() == marketAddress, ErrorCodes.MarketplaceAllowedOnly)

        transferToken!(buyer, tokenOwner, ALPH, price)
        transferTokenFromSelf!(buyer, tokenId, 1)
        destroySelf!(tokenOwner)
    }

    @using(assetsInContract = true)
    pub fn cancel() -> () {
        checkCaller!(callerAddress!() == marketAddress, ErrorCodes.MarketplaceAllowedOnly)
        destroySelf!(tokenOwner)
    }

    @using(updateFields = true)
    pub fn updatePrice(newPrice: U256) -> () {
        checkCaller!(callerAddress!() == marketAddress, ErrorCodes.MarketplaceAllowedOnly)
        price = newPrice
    }
}
