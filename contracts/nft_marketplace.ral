import "nft.ral"
import "nft_listing.ral"

TxContract NFTMarketPlace(
    nftListingTemplateId: ByteVec,
    mut admin: Address,
    mut listingFee: U256,
    mut commissionRate: U256   // basis point. e.g. 2.5% == 250 basis point
) {

    event NFTListed(
      price: U256,
      tokenId: ByteVec,
      tokenOwner: Address,
      listingContractId: ByteVec
    )

    event AdminUpdated(
        previous: Address,
        new: Address
    )

    event ListingFeeUpdated(
        previous: U256,
        new: U256
    )

    event CommissionRateUpdated(
        previous: U256,
        new: U256
    )

    @using(preapprovedAssets = true, assetsInContract = true)
    pub fn buyNFT(
        tokenId: ByteVec,
        totalPayment: U256
    ) -> () {
        let nftListingContractId = blake2b!(blake2b!(tokenId ++ selfContractId!()))
        let nftListing = NFTListing(nftListingContractId)
        let buyer = callerAddress!()
        nftListing.buy{buyer -> totalPayment}(buyer)
    }

    @using(preapprovedAssets = true, assetsInContract = true)
    pub fn listNFT(
        tokenId: ByteVec,
        price: U256
    ) -> (Address) {
        assert!(price > 0)

        // Only owner can list the NFT
        let tokenOwner = callerAddress!()
        let nft = NFT(tokenId)
        let currentTokenOwner = nft.getOwner()
        assert!(currentTokenOwner == tokenOwner)

        let initialState = encodeToByteVec!(
            price,
            tokenId,
            tokenOwner,
            selfAddress!(),
            commissionRate
        )

        // Create the listing contract
        let nftListingContractId = copyCreateSubContract!{tokenOwner -> 1 alph}(tokenId, nftListingTemplateId, initialState)

        // Charge the listing fee
        transferAlphToSelf!(tokenOwner, listingFee)

        emit NFTListed(
            price,
            tokenId,
            tokenOwner,
            nftListingContractId
        )

        return contractIdToAddress!(nftListingContractId)
    }

    pub fn cancelNFTListing(tokenId: ByteVec) -> () {
        let nftListingContractId = blake2b!(blake2b!(tokenId ++ selfContractId!()))
        let nftListing = NFTListing(nftListingContractId)

        assert!(callerAddress!() == nftListing.getTokenOwner())

        nftListing.cancel()
    }

    pub fn updateNFTPrice(tokenId: ByteVec, newPrice: U256) -> () {
        let nftListingContractId = blake2b!(blake2b!(tokenId ++ selfContractId!()))
        let nftListing = NFTListing(nftListingContractId)

        assert!(callerAddress!() == nftListing.getTokenOwner())

        nftListing.updatePrice(newPrice)
    }

    pub fn updateAdmin(newAdmin: Address) -> () {
        assert!(callerAddress!() == admin)

        emit AdminUpdated(admin, newAdmin)
        admin = newAdmin
    }

    pub fn updateListingFee(newListingFee: U256) -> () {
        assert!(callerAddress!() == admin)

        emit ListingFeeUpdated(listingFee, newListingFee)
        listingFee = newListingFee
    }

    pub fn updateCommissionRate(newCommissionRate: U256) -> () {
        assert!(callerAddress!() == admin)

        emit CommissionRateUpdated(commissionRate, newCommissionRate)
        commissionRate = newCommissionRate
    }

    pub fn getListingFee() -> U256 {
        return listingFee
    }
}