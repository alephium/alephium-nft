import "nft.ral"
import "nft_listing.ral"

Contract NFTMarketPlace(
    nftListingTemplateId: ByteVec,
    mut admin: Address,
    mut listingFee: U256,
    mut commissionRate: U256   // basis point. e.g. 2.5% == 250 basis point
) {
    event NFTListed(
      price: U256,
      tokenId: ByteVec,
      tokenOwner: Address,
      listingContractId: ByteVec
    )

    event NFTSold(
      price: U256,
      tokenId: ByteVec,
      previousOwner: Address,
      newOwner: Address
    )

    event NFTListingCancelled(
      tokenId: ByteVec,
      tokenOwner: Address
    )

    event NFTListingPriceUpdated(
       tokenId: ByteVec,
       oldPrice: U256,
       newPrice: U256
    )

    event AdminUpdated(
        previous: Address,
        new: Address
    )

    event ListingFeeUpdated(
        previous: U256,
        new: U256
    )

    event CommissionRateUpdated(
        previous: U256,
        new: U256
    )

    enum ErrorCodes {
        AdminAllowedOnly = 0
        TokenOwnerAllowedOnly = 1
        NFTPriceIsZero = 2
        CommissionIsZero = 3
    }

    @using(preapprovedAssets = true)
    pub fn buyNFT(
        tokenId: ByteVec,
        totalPayment: U256
    ) -> () {
        let nftListingContractId = blake2b!(blake2b!(selfContractId!() ++ tokenId))
        let nftListing = NFTListing(nftListingContractId)
        let buyer = callerAddress!()
        let price = nftListing.getPrice()
        let tokenOwner = nftListing.getTokenOwner()

        nftListing.buy{buyer -> totalPayment}(buyer)
        emit NFTSold(price, tokenId, tokenOwner, buyer)
    }

    @using(preapprovedAssets = true, assetsInContract = true, externalCallCheck = false)
    pub fn payCommission(buyer: Address, commission: U256) -> () {
        checkCaller!(commission > 0, ErrorCodes.CommissionIsZero)
        transferAlph!(buyer, selfAddress!(), commission)
    }

    @using(preapprovedAssets = true, assetsInContract = true)
    pub fn listNFT(
        tokenId: ByteVec,
        price: U256
    ) -> (Address) {
        assert!(price > 0, ErrorCodes.NFTPriceIsZero)

        // Only owner can list the NFT
        let tokenOwner = callerAddress!()
        let nft = NFT(tokenId)
        let currentTokenOwner = nft.getOwner()
        assert!(currentTokenOwner == tokenOwner, ErrorCodes.TokenOwnerAllowedOnly)

        let initialState = encodeToByteVec!(
            price,
            tokenId,
            tokenOwner,
            selfAddress!(),
            commissionRate
        )

        // Create the listing contract
        let nftListingContractId = copyCreateSubContract!{tokenOwner -> 1 alph}(tokenId, nftListingTemplateId, initialState)

        // Charge the listing fee
        transferAlphToSelf!(tokenOwner, listingFee)

        emit NFTListed(
            price,
            tokenId,
            tokenOwner,
            nftListingContractId
        )

        return contractIdToAddress!(nftListingContractId)
    }

    pub fn cancelNFTListing(tokenId: ByteVec) -> () {
        let nftListingContractId = blake2b!(blake2b!(selfContractId!() ++ tokenId))
        let nftListing = NFTListing(nftListingContractId)
        let tokenOwner = nftListing.getTokenOwner()

        assert!(callerAddress!() == tokenOwner, ErrorCodes.TokenOwnerAllowedOnly)

        nftListing.cancel()
        emit NFTListingCancelled(tokenId, tokenOwner)
    }

    pub fn updateNFTPrice(tokenId: ByteVec, newPrice: U256) -> () {
        let nftListingContractId = blake2b!(blake2b!(selfContractId!() ++ tokenId))
        let nftListing = NFTListing(nftListingContractId)

        assert!(callerAddress!() == nftListing.getTokenOwner(), ErrorCodes.TokenOwnerAllowedOnly)

        let price = nftListing.getPrice()

        nftListing.updatePrice(newPrice)
        emit NFTListingPriceUpdated(tokenId, price, newPrice)
    }

    pub fn updateAdmin(newAdmin: Address) -> () {
        assert!(callerAddress!() == admin, ErrorCodes.AdminAllowedOnly)

        admin = newAdmin
        emit AdminUpdated(admin, newAdmin)
    }

    pub fn updateListingFee(newListingFee: U256) -> () {
        assert!(callerAddress!() == admin, ErrorCodes.AdminAllowedOnly)

        listingFee = newListingFee
        emit ListingFeeUpdated(listingFee, newListingFee)
    }

    pub fn updateCommissionRate(newCommissionRate: U256) -> () {
        assert!(callerAddress!() == admin, ErrorCodes.AdminAllowedOnly)

        commissionRate = newCommissionRate
        emit CommissionRateUpdated(commissionRate, newCommissionRate)
    }

    @using(readonly = true)
    pub fn getListingFee() -> U256 {
        return listingFee
    }
}
